<%init>
if ( $m->dhandler_arg !~ /^[0-9a-f]{32}\.js$/ ) {
    # This doesn't look like a real request for squished JS,
    # so redirect to a more failsafe place
    Jifty->web->redirect( "/static/js/" . $m->dhandler_arg );
}

Jifty->web->generate_javascript;

use HTTP::Date ();

if ( ( Jifty->handler->cgi->http('If-Modified-Since') or Jifty->handler->cgi->http('If-None-Match') )
        and $m->dhandler_arg eq Jifty->web->cached_javascript_digest . '.js' )
{
    Jifty->log->debug("Returning 304 for cached javascript");
    $r->header_out( Status => 304 );
    return;
}

$r->content_type("application/x-javascript");
$r->header_out( 'Expires' => HTTP::Date::time2str(time + 31536000) );
$r->header_out( 'Last-Modified' => HTTP::Date::time2str( Jifty->web->cached_javascript_time ) );
$r->header_out( 'Age' => time - Jifty->web->cached_javascript_time );
$r->header_out( 'Etag' => Jifty->web->cached_javascript_digest );

# XXX TODO: If we start caching the squished JS in a file somewhere, we
# can have the static handler serve it, which would take care of gzipping
# for us.
use Compress::Zlib qw();

if ( Jifty::View::Static::Handler->client_accepts_gzipped_content ) {
    Jifty->log->debug("Sending gzipped squished JS");
    $r->header_out( "Content-Encoding" => "gzip" );
    binmode STDOUT;
    print Compress::Zlib::memGzip( Jifty->web->cached_javascript );
}
else {
    Jifty->log->debug("Sending squished JS");
    print Jifty->web->cached_javascript;
}
return;
</%init>
